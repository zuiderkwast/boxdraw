#!/usr/bin/perl -T

# boxdraw - a plain text drawing program for the terminal
# Copyright (C) 2019  Viktor Söderqvist
#
# This program is free software: you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation, either version 3 of the License, or
# (at your option) any later version.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with this program.  If not, see <http://www.gnu.org/licenses/>.

use strict;
use warnings;
use utf8;
use 5.18.0;
use open qw(:std :utf8);
my ($cols, $lines);       # size of terminal
my ($x, $y) = (1, 1);     # cursor coordinates, 1-based
my ($selx, $sely);        # selection starting point or (under, undef)
my ($y1, $x1, $y2, $x2) = (1,1,1,1);  # selection rectangle
my $sticky_selection = 0; # when on, arrow keys extend selection
my $message = "";         # message to display in status bar
my $rows = [];            # text lines without \n
my $debug = 0;            # toggled by -d or --debug
my $show_splash = 1;      # spash screen at startup, disable with --no-splash
my $filename;             #
my $selector;             # Visible 'stroke' (or e.g. 'character') selector
my $mode = 'draw';        # 'draw', 'move', 'help', 'text'
my $help_page_no = 0;     # Page number (module number of pages)
my $selected_stroke = 0;  # index in @$stroke_buttons

my $stroke_buttons = [
    ['┌─┐',
     '└─┘', 'single'],
    ['╭─╮',
     '╰─╯', 'single-rounded'],
    ['┏━┓',
     '┗━┛', 'bold'],
    ['╔═╗',
     '╚═╝', 'double'],
    ['┌╌┐',
     '└╌┘', 'single-bashed'],
    ['╭╌╮',
     '╰╌╯', 'single-bashed-rounded'],
    ['┏╍┓',
     '┗╍┛', 'bold-bashed'],
    ['┌┄┐',
     '└┄┘', 'single-dashed'],
    ['╭┄╮',
     '╰┄╯', 'single-dashed-rounded'],
    ['┏┅┓',
     '┗┅┛', 'bold-dashed'],
    ['┌┈┐',
     '└┈┘', 'single-dotted'],
    ['╭┈╮',
     '╰┈╯', 'single-dotted-rounded'],
    ['┏┉┓',
     '┗┉┛', 'bold-dotted'],
];

#open my $tty, "<+", "/dev/tty";

# TODO: Read from /dev/tty instead of STDIN.
#       Use STDIN for initial content.
die "STDIN is not a tty" unless -t STDIN;
# TODO: Write to /dev/tty instead of STDOUT.
#       Write content to STDOUT in some situation.
die "STDOUT is not a tty" unless -t STDOUT;

# Read ARGV
while ($_ = shift) {
    if (/^-d|--debug$/)  { $debug = 1; next };
    if (/^--no-splash$/) { $show_splash = 0; next };
    if (/^--$/)          { $filename = shift; last };
    if (/^[^-]/)         { $filename = $_; next };
    die "Unrecognized option: $_";
}
$show_splash = 0 if defined $filename;
$ENV{PATH} = '/usr/bin:/bin';

sub debug {
    print STDERR @_ if $debug;
}

sub safe_backticks(@) {
    open my $fd, "-|", @_ or die "Can't execute $_[0]";
    local $/ = undef;
    return <$fd>;
}

sub update_window_size() {
    my $stty = safe_backticks qw(stty -F/dev/tty -a);
    $stty =~ /rows (\d+); columns (\d+)/ or die "Can't get terminal size";
    $cols = int $2;
    $lines = int $1;
}

sub winch() {
    update_window_size();
    message("Window size: ${cols}x${lines}");
    $x = $cols if $x > $cols;
    $y = $lines - 1 if $y > $lines -1 ;
    render();
}
use sigtrap 'handler', \&winch, 'WINCH';
use sigtrap qw(die normal-signals);
use sigtrap qw(stack-trace error-signals);
system qw(tput smcup); # alternate screen

END {
    system qw(stty sane);
    print "\e[?25h"; # show the cursor
    print "\e[?2004l"; # bracketed paste mode off
    #print "\n";
    system qw(tput rmcup); # exit alternate screen
};

# --- rendering ---

sub render_stroke_selector {
    return if $lines < 4;
    print "\e[", $lines - 3 , ";1H\e[30;103m";
    printf "%-*s", $cols, "Stroke";
    my $n = @$stroke_buttons;
    for (my $line = 0; $line < 2; $line++) {
        my $r = $lines - 2 + $line;
        print "\e[$r;1H";
        print "\e[30;103m";
        # Print a line for each of the strokes
        for (my $i = 0; $i < $n; $i++) {
            # blue (selected) or green
            print "\e[44m" if $i == $selected_stroke;
            print $$stroke_buttons[$i][$line];
            print "\e[103m" if $i == $selected_stroke;
        }
        # Print padding for the rest of the line
        print " " x ($cols - 3 * $n);
    }
    print "\e[m"; # reset/normal
}

sub render_status_bar () {
    my $left_msg = " " . ($message ne "" ? $message :
                          $mode eq "draw" ? "h = help, q = quit" :
                          $mode eq "text" ? "INSERT = exit text mode" :
                          "");
    my $stroke_ind = $$stroke_buttons[$selected_stroke][0];
    my @right_msgs = ($mode);
    if ($x2 != $x1 || $y2 != $y1) {
        push @right_msgs, ($x2 - $x1 + 1) . "x" . ($y2 - $y1 + 1);
    }
    push @right_msgs, sprintf("%2s,%-2s", $y, $x);
    push @right_msgs, $stroke_ind;
    my $right_msg = join(' ', @right_msgs) . ' ';
    my $left_length = $cols - length($right_msg);
    my $padded_msg = length($left_msg) > $left_length ?
                         substr($left_msg, 0, $left_length) :
                         sprintf("%-*s", $left_length, $left_msg);
    $padded_msg .= $right_msg;

    my $message_y = $lines;
    my $color = $message        ? "102" :   # green (was 101=red)
                $mode eq "text" ? "104" :   # blue
                                  "103";    # yellow
    printf "\e[$message_y;1H\e[30;${color}m$padded_msg\e[m"; #49
}

sub render_popup($$) {
    my ($text, $color_esc_seq) = @_;
    my @l = $text =~ /\S.+\S/gm;
    my $off_x = int(($cols - length($l[0])) / 2) + 1;
    my $off_y = int(($lines - @l) / 2) + 1;
    print $color_esc_seq if $color_esc_seq;
    foreach (@l) {
        print "\e[$off_y;${off_x}H$_";
        $off_y++;
    }
    print "\e[m" if $color_esc_seq;  # reset color
    print "\e[?25l";                 # hide cursor
}

sub render_splash() {
    state $splash = <<'EOT';
╭───────────────────────────────────────────╮
│                                           │
│       ╓┰─────────────┰─────────────╴      │
│       ║┃             ┃ ════════════╗      │
│       ║┠──╮╭──╮╲ ╱╭──┦┎──╮╭──┒╷ ╻ ╷║      │
│       ║┃▒░││▒░│ ╳ │▒░│┃   │▒░┃│ ┃ │║      │
│       ║┖──╯╰──╯╱ ╲╰──╯╹   ╰──┚╰─┸─╯║      │
│       ╚════════════════════════════╝      │
│                                           │
┝━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━┥
│               PRESS ANY KEY               │
╰───────────────────────────────────────────╯
EOT
    render_popup($splash, "\e[30;105m");
}

sub render_help() {
    state $pages = [<<'EOT',
╭───────────────────────────────────────────╮
│ Help                                      │
┝━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━┥
│ h               Help                      │
│ q               Quit                      │
│ ARROWS          Navigate                  │
│ CTRL + ARROWS   Draw                      │
│ TAB             Select stroke             │
│ SHIFT + ARROWS  Select area               │
│ RETURN          Draw borders in selection │
│ DELETE          Clear selected area       │
┝━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━┥
│                 h = hide      n = next    │
╰───────────────────────────────────────────╯
EOT
        <<'EOT',
╭───────────────────────────────────────────╮
│ Help: Box-drawing                         │
┝━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━┥
│ -               ──── Single stroke    ╶┐  │
│ b               ━━━━ Bold stroke          │
│ =               ════ Double stroke        │
│ o               ──── Rounded corners  ╶╮  │
│ TAB             Stroke menu               │
│ RETURN          Draw borders in selection │
│                                           │
│ x, /, \         Draw diagonals  ╲ ╱    ╱  │
│                 in quare         ╳    ╱   │
│                 selection       ╱ ╲  ╱    │
│                                           │
┝━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━┥
│ p = previous    h = hide      n = next    │
╰───────────────────────────────────────────╯
EOT
        <<'EOT',
╭───────────────────────────────────────────╮
│ Help: Select, copy & paste                │
┝━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━┥
│ SHIFT + ARROWS  Select                    │
│ SPACE, ARROWS   Select, "sticky"          │
│ SPACE           Deselect                  │
│ CTRL + c        Copy to X clipboard       │
│ CTRL + x        NOT IMPLEMENTED (cut)     │
│ CTRL + v        NOT IMPLEMENTED (paste)   │
│ CTRL + s        NOT IMPLEMENTED (save)    │
│ m, LEFT ARROW   Move/pull (experimental,  │
│                 will definitely change)   │
┝━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━┥
│ p = previous    h = hide      n = next    │
╰───────────────────────────────────────────╯
EOT
        <<'EOT',
╭───────────────────────────────────────────╮
│ Help: Semigraphics, 2×2 subdivision       │
┝━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━┥
│ 1, 3, 7, 9,     Quarter block, toggle     │
│ 2, 4, 6, 8,     Half block                │
│ 5               Full block                │
│                                           │
│ Use numpad!                               │
│ ░░░  ░░░  ░░░          Press multiple     │
│ ░▘░7 ░▀░8 ░▝░9         times to compose   │
│ ░░░  ░░░  ░░░          all possible       │
│                        combinations:      │
│ ░░░  ░░░  ░░░                             │
│ ░▌░4 ░█░5 ░▐░6         ░░░░░░░░░░░░░░░░░  │
│ ░░░  ░░░  ░░░          ░ ░▝░▘░▀░▗░▐░▚░▜░  │
│                        ░░░░░░░░░░░░░░░░░  │
│ ░░░  ░░░  ░░░          ░▖░▞░▌░▛░▄░▟░▙░█░  │
│ ░▖░1 ░▄░2 ░▗░3         ░░░░░░░░░░░░░░░░░  │
│ ░░░  ░░░  ░░░                             │
│                                           │
┝━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━┥
│ p = previous    h = hide      n = next    │
╰───────────────────────────────────────────╯
EOT
        <<'EOT',
╭───────────────────────────────────────────╮
│ Help: More block characters               │
┝━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━┥
│ 2, 4, 6, 8      When repeated, thin block │
│                 on each side              │
│ 5               When repeated, alternate  │
│                 filled blocks             │
│ Use numpad!      ▁▁  ▁▁  ▁▁  ▁▁  ▁▁       │
│      ░░░        ▕██▏▕▓▓▏▕▒▒▏▕░░▏▕  ▏      │
│      ░▔░8        ▔▔  ▔▔  ▔▔  ▔▔  ▔▔       │
│      ░░░                                  │
│                                           │
│ ░░░  ░░░  ░░░                             │
│ ░▏░4 ░▓░5 ░▕░6                            │
│ ░░░  ░░░  ░░░                             │
│                                           │
│      ░░░                                  │
│      ░▁░2                                 │
│      ░░░                                  │
│                                           │
┝━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━┥
│ p = previous    h = hide      n = next    │
╰───────────────────────────────────────────╯
EOT
        <<'EOT',
╭───────────────────────────────────────────╮
│ Help: Block dragrams                      │
┝━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━┥
│                                           │
│                   012345678               │
│ u (repeatedly)   ░░░░░░░░░░░  Upward      │
│ U (backwards)    ░ ▁▂▃▄▅▆▇█░  diagram     │
│                  ░░░░░░░░░░░              │
│                                           │
│                  ░░░                      │
│ r (repeatedly)   ░█░ 8        Left-to-    │
│ R (backwards)    ░▉░ 7        right       │
│                  ░▊░ 6        diagram     │
│                  ░▋░ 5                    │
│                  ░▌░ 4                    │
│                  ░▍░ 3                    │
│                  ░▎░ 2        This        │
│                  ░▏░ 1        should also │
│                  ░ ░ 0        work with a │
│                  ░░░          selection.  │
│                                           │
┝━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━┥
│ p = previous    h = hide                  │
╰───────────────────────────────────────────╯
EOT
    ];
    # Sanitize page number too within existing pages
    $help_page_no = 0        if $help_page_no < 0;
    $help_page_no = $#$pages if $help_page_no > $#$pages;
    # Render the page
    render_popup($pages->[$help_page_no], "\e[30;47m");
}

sub render_content() {

    my $sel_color = $mode eq 'move' ? "\e[45m"  # purple
                                    : "\e[43m"; # yellow

    my $last_content_line = defined($selector) ? $lines - 4 : $lines - 1;
    for (my $r = 1; $r <= $last_content_line; $r ++) {
        my $str = defined $$rows[$r - 1] ? $$rows[$r - 1] : "";
        if ($r >= $y1 && $r <= $y2 && ($x1 != $x2 || $y1 != $y2)) {
            # Highlight selection (rectangle) with background color
            # except if the selection is 1x1.
            # Pad $str with spaces so that it covers the selection
            if (length($str) < $x2) {
                $str .= " " x ($x2 - length($str));
            }
            print "\e[$r;1H",
                  substr($str, 0, $x1 - 1), $sel_color,           # highlight
                  substr($str, $x1 - 1, $x2 - $x1 + 1), "\e[49m", # normal
                  substr($str, $x2);
        } elsif ($str ne "") {
            # No selection. Just print the line.
            print "\e[$r;1H$str";
        }
    }
}

sub render() {
    # Clear screen
    print "\e[2J"; # Erase in Display
    #"\ec" Reset to Initial State (too brutal)

    # TODO: Move show cursor to where it should be, e.g. when closing
    # help box, etc. It was implicitly here when we did "\ec".
    print "\e[?25h"; # show the cursor

    render_content();
    render_status_bar();

    if ($mode eq "help") {
        render_help();
    } elsif (!defined $selector) {
        # Set cursor position
        print "\e[$y;${x}H";
    } elsif ($selector eq 'stroke') {
        render_stroke_selector();
    }
    flush STDOUT;
}

# --- content manipulation ---

sub get_substr_at_yx($$$) {
    my ($y, $x, $length) = (@_);
    $x--; $y--; # switch to 0-based index
    if (defined $$rows[$y] && length($$rows[$y]) > $x) {
        my $str = substr($$rows[$y], $x, $length);
        # pad with spaces
        $str = sprintf("%-*s", $length, $str) if length($str) < $length;
        return $str;
    } else {
        return " " x $length;
    }
}

sub get_char_at_yx($$) {
    my ($y, $x) = (@_);
    return get_substr_at_yx($y, $x, 1);
}

sub set_substr_at_yx($$$) {
    # This function doesn't create trailing whitespace
    my ($s, $y, $x) = (@_);
    $x--; $y--; # switch to 0-based index
    unless (defined $$rows[$y]) {
        return if $s eq " ";
        $$rows[$y] = "";
    }
    my $len = length $$rows[$y];
    if ($x >= $len) {
        return if $s eq " ";
        my $pad = " " x ($x - $len);
        $$rows[$y] .= $pad . $s;
    } elsif ($s eq " " && $x + 1 == $len) {
        # Optimized case: Overwriting the last character with space
        # Remove it and trim.
        $$rows[$y] =~ s/ *.$//;
    } else {
        # Replace substr
        substr($$rows[$y], $x, length($s), $s);
        # Trim railing spaces (only needed if $s ends with a space)
        $$rows[$y] =~ s/ *$//;
    }
}
sub set_char {
    my ($s) = (@_);
    set_substr_at_yx($s, $y, $x);
}

sub fill_selection {
    my ($s) = (@_);
    my $repeated_s = $s x (($x2 - $x1 + 1) / length($s));
    for (my $r = $y1; $r <= $y2; $r++) {
        set_substr_at_yx($repeated_s, $r, $x1);
    }
    $sticky_selection = 0;
}

sub rotate_fill_selection(@) {
    my $current = get_char_at_yx($y1, $x1);
    my $fill = $_[0];
    for (my $i = 0; $i < @_; $i++) {
        if ($current eq $_[$i]) {
            $fill = $_[($i + 1) % @_];
            last;
        }
    }
    fill_selection($fill);
}

# Order the coordinates of a rectangle as
# y1, x1, y2, x2 => (min y, min x, max y, max x)
sub normalize_area {
    my ($y1, $x1, $y2, $x2) = (@_);
    ($x1, $x2) = $x1 <= $x2 ? ($x1, $x2) : ($x2, $x1);
    ($y1, $y2) = $y1 <= $y2 ? ($y1, $y2) : ($y2, $y1);
    return ($y1, $x1, $y2, $x2);
}

# Sets selection rectangle to the area between (sely,selx) and (y,x)
sub update_selection {
    if (defined $selx) {
        # Selection exists
        ($y1, $x1, $y2, $x2) = normalize_area($y, $x, $sely, $selx);
    } else {
        ## Set an empty selection
        #$x1 = $y1 = 1;
        #$x2 = $y2 = 0;
        # Select 1x1 at cursor
        $x1 = $x2 = $x;
        $y1 = $y2 = $y;
    }
}

sub deselect {
    $selx = $sely = undef;
    $sticky_selection = 0;
    update_selection;
}

# order: ABCD (up, down, right, left)
my %strokes = (
    "single" => "╵╷╶╴",
    "bold"   => "╹╻╺╸",
    "double" => "║║══", # single direction missing
);

# manipulation of single-direction components
# (Used for experimental 'move' mode)
my %point_down = (
    "╵" => "╷", "╷" => "╷", "╶" => "╷", "╴" => "╷",
    "╹" => "╻", "╻" => "╻", "╺" => "╻", "╸" => "╻",
    "║" => "║", "═" => "║");
my %point_up = (
    "╵" => "╵", "╷" => "╵", "╶" => "╵", "╴" => "╵",
    "╹" => "╹", "╻" => "╹", "╺" => "╹", "╸" => "╹",
    "║" => "║", "═" => "║");
my %point_right = (
    "╵" => "╶", "╷" => "╶", "╶" => "╶", "╴" => "╶",
    "╹" => "╺", "╻" => "╺", "╺" => "╺", "╸" => "╺",
    "║" => "═", "═" => "═");
my %point_left = (
    "╵" => "╴", "╷" => "╴", "╶" => "╴", "╴" => "╴",
    "╹" => "╸", "╻" => "╸", "╺" => "╸", "╸" => "╸",
    "║" => "═", "═" => "═");
#my %mirrored = (
#    "╵" => "╷", "╷" => "╵", "╶" => "╴", "╴" => "╶",
#    "╹" => "╻", "╻" => "╹", "╺" => "╸", "╸" => "╺",
#    "║" => "║", "═" => "═");

my %decomposed = (
    "─" => "  ╶╴",
    "━" => "  ╺╸",
    "│" => "╵╷  ",
    "┃" => "╹╻  ",
    "┌" => " ╷╶ ",
    "┍" => " ╷╺ ",
    "┎" => " ╻╶ ",
    "┏" => " ╻╺ ",
    "┐" => " ╷ ╴",
    "┑" => " ╷ ╸",
    "┒" => " ╻ ╴",
    "┓" => " ╻ ╸",
    "└" => "╵ ╶ ",
    "┕" => "╵ ╺ ",
    "┖" => "╹ ╶ ",
    "┗" => "╹ ╺ ",
    "┘" => "╵  ╴",
    "┙" => "╵  ╸",
    "┚" => "╹  ╴",
    "┛" => "╹  ╸",
    "├" => "╵╷╶ ",
    "┝" => "╵╷╺ ",
    "┞" => "╹╷╶ ",
    "┟" => "╵╻╶ ",
    "┠" => "╹╻╶ ",
    "┡" => "╹╷╺ ",
    "┢" => "╵╻╺ ",
    "┣" => "╹╻╺ ",
    "┤" => "╵╷ ╴",
    "┥" => "╵╷ ╸",
    "┦" => "╹╷ ╴",
    "┧" => "╵╻ ╴",
    "┨" => "╹╻ ╴",
    "┩" => "╹╷ ╸",
    "┪" => "╵╻ ╸",
    "┫" => "╹╻ ╸",
    "┬" => " ╷╶╴",
    "┭" => " ╷╶╸",
    "┮" => " ╷╺╴",
    "┯" => " ╷╺╸",
    "┰" => " ╻╶╴",
    "┱" => " ╻╶╸",
    "┲" => " ╻╺╴",
    "┳" => " ╻╺╸",
    "┴" => "╵ ╶╴",
    "┵" => "╵ ╶╸",
    "┶" => "╵ ╺╴",
    "┷" => "╵ ╺╸",
    "┸" => "╹ ╶╴",
    "┹" => "╹ ╶╸",
    "┺" => "╹ ╺╴",
    "┻" => "╹ ╺╸",
    "┼" => "╵╷╶╴",
    "┽" => "╵╷╶╸",
    "┾" => "╵╷╺╴",
    "┿" => "╵╷╺╸",
    "╀" => "╹╷╶╴",
    "╁" => "╵╻╶╴",
    "╂" => "╹╻╶╴",
    "╃" => "╹╷╶╸",
    "╄" => "╹╷╺╴",
    "╅" => "╵╻╶╸",
    "╆" => "╵╻╺╴",
    "╇" => "╹╷╺╸",
    "╈" => "╵╻╺╸",
    "╉" => "╹╻╶╸",
    "╊" => "╹╻╺╴",
    "╋" => "╹╻╺╸",
    "═" => "  ══",
    "║" => "║║  ",
    "╒" => " ╷═ ",
    "╓" => " ║╶ ",
    "╔" => " ║═ ",
    "╕" => " ╷ ═",
    "╖" => " ║ ╴",
    "╗" => " ║ ═",
    "╘" => "╵ ═ ",
    "╙" => "║ ╶ ",
    "╚" => "║ ═ ",
    "╛" => "╵  ═",
    "╜" => "║  ╴",
    "╝" => "║  ═",
    "╞" => "╵╷═ ",
    "╟" => "║║╶ ",
    "╠" => "║║═ ",
    "╡" => "╵╷ ═",
    "╢" => "║║ ╴",
    "╣" => "║║ ═",
    "╤" => " ╷══",
    "╥" => " ║╶╴",
    "╦" => " ║══",
    "╧" => "╵ ══",
    "╨" => "║ ╶╴",
    "╩" => "║ ══",
    "╪" => "╵╷══",
    "╫" => "║║╶╴",
    "╬" => "║║══",
    "╴" => "   ╴",
    "╵" => "╵   ",
    "╶" => "  ╶ ",
    "╷" => " ╷  ",
    "╸" => "   ╸",
    "╹" => "╹   ",
    "╺" => "  ╺ ",
    "╻" => " ╻  ",
    "╼" => "  ╺╴",
    "╽" => "╵╻  ",
    "╾" => "  ╶╸",
    "╿" => "╹╷  ",
);

# The inverse of decompose
my %composed = map { $decomposed{$_} => $_ } keys %decomposed;
$composed{"║   "} = "║";
$composed{" ║  "} = "║";
$composed{"  ═ "} = "═";
$composed{"   ═"} = "═";

# Add the rounded corners and dashed/dotted after the composed ones (hash flip)
# have been calculated
$decomposed{"╭"} = " ╷╶ ";
$decomposed{"╮"} = " ╷ ╴";
$decomposed{"╰"} = "╵ ╶ ";
$decomposed{"╯"} = "╵  ╴";

$decomposed{"┄"} = $decomposed{"┈"} = $decomposed{"╌"} = "  ╶╴";
$decomposed{"┅"} = $decomposed{"┉"} = $decomposed{"╍"} = "  ╺╸";
$decomposed{"┆"} = $decomposed{"┊"} = $decomposed{"╎"} = "╵╷  ";
$decomposed{"┇"} = $decomposed{"┋"} = $decomposed{"╏"} = "╹╻  ";

# Conversion to rounded corners and dashed/dotted is done afterwards
my %rounded = ("┌" => "╭", "┐" => "╮", "└" => "╰", "┘" => "╯");
my %dotted  = ("│" => "┊", "─" => "┈", "┃" => "┋", "━" => "┉");
my %dashed  = ("│" => "┆", "─" => "┄", "┃" => "┇", "━" => "┅");
my %bashed  = ("│" => "╎", "─" => "╌", "┃" => "╏", "━" => "╍");

# Combines decomposed box-drawing characters
# For each position, take from the first string unless it's a space.
# "a  d", "ab y" => "ab d"
sub combine_decomposed {
    my ($as, $bs) = (@_);
    my (@a) = split //, $as;
    my (@b) = split //, $bs;
    my $c = "";
    for (my $i = 0; $i < 4; $i++) {
        $c .= $a[$i] eq " " ? $b[$i] : $a[$i];
    }
    return $c;
}

# $decomposed is a 4 character string. $stroke is e.g. 'single'
# Returns a decomposed character with the given stroke
sub replace_decomposed_with_stroke {
    my ($composed, $stroke) = (@_);
    my (@as) = split //, $composed;
    my (@bs) = split //, $strokes{$stroke};
    my $res = "";
    for (my $i = 0; $i < 4; $i++) {
        $res .= $as[$i] ne " " ? $bs[$i] : $as[$i];
    }
    return $res;
}

# Decompose a "═" character into its four directions, using its
# neighbours to guess if it's "  ═ ", "   ═" or "  ══".
sub decompose_double_horizontal_at_yx {
    my ($y, $x) = (@_);
    my $left_decomp  = $decomposed{get_char_at_yx($y, $x - 1)};
    my $right_decomp = $decomposed{get_char_at_yx($y, $x + 1)};
    my $has_conn_left  = defined $left_decomp  && $left_decomp  =~ /^..═.$/;
    my $has_conn_right = defined $right_decomp && $right_decomp =~ /^...═$/;
    return "   ═" if $has_conn_left && !$has_conn_right;
    return "  ═ " if !$has_conn_left && $has_conn_right;
    return "  ══";
}

# Decompose a "║" character into its four directions, using its
# neighbours to guess if it's "║   ", " ║  " or "║║  ".
sub decompose_double_vertical_at_yx {
    my ($y, $x) = (@_);
    my $above_decomp = $decomposed{get_char_at_yx($y - 1, $x)};
    my $below_decomp = $decomposed{get_char_at_yx($y + 1, $x)};
    my $has_conn_above = defined $above_decomp && $above_decomp =~ /^.║..$/;
    my $has_conn_below = defined $below_decomp && $below_decomp =~ /^║...$/;
    return "║   " if $has_conn_above && !$has_conn_below;
    return " ║  " if !$has_conn_above && $has_conn_below;
    return "║║  ";
}

# Returns a string of 4 characters if the character is a box-drawing character;
# undef otherwise.
sub get_decomposed_at_yx($$) {
    my ($y, $x) = (@_);
    my $old = get_char_at_yx($y, $x);
    return
        $old eq "═" ? decompose_double_horizontal_at_yx($y, $x) :
        $old eq "║" ? decompose_double_vertical_at_yx($y, $x) :
        $decomposed{$old};
}

# Draws a box-drawing character at a given coordinate, combining it with the
# character already drawn at that coordinate. $c is a character given as single
# stroke with square corners, which is converted to the stroke given by $stroke
# before combining it with the character already present at the coordinare.
sub draw_at_yx {
    my ($c, $y, $x, $stroke) = (@_);

    my $is_rounded = $stroke =~ /rounded/;
    my $is_dotted  = $stroke =~ /dotted/;
    my $is_dashed  = $stroke =~ /dashed/;
    my $is_bashed  = $stroke =~ /bashed/;

    # strip modifiers, e.g. "single-dashed-rounded" => "single"
    $stroke =~ s/-.*//;

    # Decompose old character, e.g. "┐" into " ╷ ╴"
    # Workaround for missing half-versions of "║" and "═"
    my $old_decomposed = get_decomposed_at_yx($y, $x);

    # Decompose $c, e.g. "│" into "╵╷  " ($c is always single stroke)
    my $c_decomposed = $decomposed{$c};

    # Convert to desired stroke, e.g. "╵╷  " into "╹╻  "
    #$stroke = "single" unless $stroke;
    if ($stroke ne "single") {
        $c_decomposed = replace_decomposed_with_stroke($c_decomposed, $stroke);
    }
    my $new;
    if ($old_decomposed) {
        # Combine $old_decomposed and $c_decomposed
        # e.g. " ╷ ╴" and "╹╻  " into "╹╻ ╴"
        my $new_decomposed = combine_decomposed($c_decomposed, $old_decomposed);
        $new = $composed{$new_decomposed};
        unless ($new) {
            # Still not possible. Replace old strokes with current stroke.
            $old_decomposed = replace_decomposed_with_stroke($old_decomposed,
                                                             $stroke);
            $new_decomposed = combine_decomposed($c_decomposed, $old_decomposed);
            $new = $composed{$new_decomposed};
        }
        $new = $c unless $new; # Still not possible. Just overwrite.
    } elsif ($stroke eq "single") {
        # Optimization; not really necessary case
        $new = $c;
    } else {
        # There was no old character. Just compose $c_decomposed.
        $new = $composed{$c_decomposed};
    }
    # Replace with rounded variants if possible and desired
    if ($is_rounded && exists $rounded{$new}) {
        $new = $rounded{$new};
    } elsif ($is_dotted && exists $dotted{$new}) {
        $new = $dotted{$new};
    } elsif ($is_dashed && exists $dashed{$new}) {
        $new = $dashed{$new};
    } elsif ($is_bashed && exists $bashed{$new}) {
        $new = $bashed{$new};
    }
    set_substr_at_yx($new, $y, $x); # unless $old eq $new;
}

# Draws borders around (inside) selection and deselects
sub draw_borders_at_selection {
    draw_borders($y1, $x1, $y2, $x2, @_);
}

sub draw_selected_stroke_at {
    my ($y1, $x1, $y2, $x2) = (@_);
    ($y1, $x1, $y2, $x2) = normalize_area($y1, $x1, $y2, $x2);
    my $stroke = $$stroke_buttons[$selected_stroke][2];
    draw_borders($y1, $x1, $y2, $x2, $stroke);
}

sub draw_borders {
    my ($y1, $x1, $y2, $x2, $stroke) = (@_);

    #       0  1  2  3  4  5  6  7  8  9  A  B  C  D  E  F
    #U+250x ─  ━  │  ┃  ┄  ┅  ┆  ┇  ┈  ┉  ┊  ┋  ┌  ┍  ┎  ┏
    #U+251x ┐  ┑  ┒  ┓  └  ┕  ┖  ┗  ┘  ┙  ┚  ┛  ├  ┝  ┞  ┟
    #U+252x ┠  ┡  ┢  ┣  ┤  ┥  ┦  ┧  ┨  ┩  ┪  ┫  ┬  ┭  ┮  ┯
    #U+253x ┰  ┱  ┲  ┳  ┴  ┵  ┶  ┷  ┸  ┹  ┺  ┻  ┼  ┽  ┾  ┿
    #U+254x ╀  ╁  ╂  ╃  ╄  ╅  ╆  ╇  ╈  ╉  ╊  ╋  ╌  ╍  ╎  ╏
    #U+255x ═  ║  ╒  ╓  ╔  ╕  ╖  ╗  ╘  ╙  ╚  ╛  ╜  ╝  ╞  ╟
    #U+256x ╠  ╡  ╢  ╣  ╤  ╥  ╦  ╧  ╨  ╩  ╪  ╫  ╬  ╭  ╮  ╯
    #U+257x ╰  ╱  ╲  ╳  ╴  ╵  ╶  ╷  ╸  ╹  ╺  ╻  ╼  ╽  ╾  ╿     

    if ($y1 < $y2 && $x1 < $x2) {
        # rectangle
        draw_at_yx("┌", $y1, $x1, $stroke);
        for (my $x = $x1 + 1; $x < $x2; $x++) {
            draw_at_yx("─", $y1, $x, $stroke);
        }
        draw_at_yx("┐", $y1, $x2, $stroke);
        for (my $r = $y1 + 1; $r < $y2; $r++) {
            draw_at_yx("│", $r, $x1, $stroke);
            draw_at_yx("│", $r, $x2, $stroke);
        }
        draw_at_yx("└", $y2, $x1, $stroke);
        for (my $x = $x1 + 1; $x < $x2; $x++) {
            draw_at_yx("─", $y2, $x, $stroke);
        }
        draw_at_yx("┘", $y2, $x2, $stroke);
    } elsif ($y1 == $y2 && $x1 < $x2) {
        # horizontal line
        draw_at_yx("╶", $y1, $x1, $stroke);
        for (my $x = $x1 + 1; $x < $x2; $x++) {
            draw_at_yx("─", $y1, $x, $stroke);
        }
        draw_at_yx("╴", $y1, $x2, $stroke);
    } elsif ($y1 < $y2 && $x1 == $x2) {
        # vertical line
        draw_at_yx("╷", $y1, $x1, $stroke);
        for (my $r = $y1 + 1; $r < $y2; $r++) {
            draw_at_yx("│", $r, $x1, $stroke);
        }
        draw_at_yx("╵", $y2, $x1, $stroke);
    }
    deselect();
}

# Decomposed in the order nw, ne, sw, se
my %decomposed_block = (
    "▘" => "▘   ",
    "▝" => " ▝  ",
    "▀" => "▘▝  ",
    "▗" => "   ▗",
    "▐" => " ▝ ▗",
    "▚" => "▘  ▗",
    "▜" => "▘▝ ▗",
    "▖" => "  ▖ ",
    "▞" => " ▝▖ ",
    "▌" => "▘ ▖ ",
    "▛" => "▘▝▖ ",
    "▄" => "  ▖▗",
    "▟" => " ▝▖▗",
    "▙" => "▘ ▖▗",
    "█" => "▘▝▖▗",
);
my %composed_block =
    map { $decomposed_block{$_} => $_ } keys %decomposed_block;

sub toggle_subblock($@) {
    my $toggle_subblock = shift;
    my $old = get_char_at_yx($y1, $x1);
    my $new;
    if ($old eq $toggle_subblock) {
        # rotate between alternative blocks or " " finally
        $new = @_ ? $_[0] : " ";
        for (my $i = 0; $i < @_; $i++) {
            if ($old eq $_[$i]) {
                $new = $i < $#_ ? $_[$i + 1] : " ";
                last;
            }
        }
    } else {
        my $decomposed = $decomposed_block{$old};
        if ($decomposed) {
            my @decomp = split //, $decomposed;
            my @mask = split //, $decomposed_block{$toggle_subblock};
            # XOR with mask
            for (my $i = 0; $i < 4; $i++) {
                if ($mask[$i] ne " ") {
                    $decomp[$i] = $decomp[$i] eq " " ? $mask[$i] : " ";
                }
            }
            my $decomposed = join '', @decomp;
            $new = $composed_block{$decomposed};
        } else {
            $new = $toggle_subblock;
        }
    }
    fill_selection($new);
}

## Draws a big X over the selected area using diagonal box drawing characters.
## Input "X", "/" or "\" to draw one or both diagonals
sub draw_diagonal($) {
    return unless $y1 <= $y2 && $x1 <= $x2;

    my $mode = shift;

    # Make square
    my $delta_y = $y2 - $y1;
    my $delta_x = $x2 - $x1;
    my ($y_start, $y_end, $x_start, $x_end) = ($y1, $y2, $x1, $x2);
    if ($delta_y < $delta_x) {
        # Too wide. Crop the left and right sides.
        $x_start = $x1 + int(($delta_x - $delta_y) / 2);
        $x_end = $x_start + $delta_y;
    } elsif ($delta_y > $delta_x) {
        # Too high. Crop top and bottom.
        $y_start = $y1 + int(($delta_y - $delta_x) / 2);
        $y_end = $y_start + $delta_x;
    }
    for (my $r = $y_start; $r <= $y_end; $r++) {
        my $left = $x_start + ($r - $y_start);
        my $right = $x_end - ($r - $y_start);
        if ($left == $right) {
            set_substr_at_yx("╳", $r, $left) if $mode eq "X";
            set_substr_at_yx("╲", $r, $left) if $mode eq "\\";
            set_substr_at_yx("╱", $r, $left) if $mode eq "/";
        } else {
            set_substr_at_yx("╲", $r, $left) if $mode eq "X" || $mode eq "\\";
            set_substr_at_yx("╱", $r, $right) if $mode eq "X" || $mode eq "/";;
        }
    }
}

sub draw_bracket_at_yyx($$$$) {
    # Brackets and integral sign in Miscellaneous Technical (U+2300-U+23FF)
    state $brackets = "{}()[]S"; # in the same order as in $gfx
    state $gfx = [["⎧", "⎫", "⎛", "⎞", "⎡", "⎤", "⌠"],
                  ["⎪", "⎪", "⎜", "⎟", "⎢", "⎥", "⎮"],
                  ["⎨", "⎬", "⎜", "⎟", "⎢", "⎥", "⎮"],
                  ["⎪", "⎪", "⎜", "⎟", "⎢", "⎥", "⎮"],
                  ["⎩", "⎭", "⎝", "⎠", "⎣", "⎦", "⌡"]];
    my ($bracket, $y1, $y2, $x) = (@_);
    my $i = $brackets =~ /\Q$bracket\E/ ? length($`) : die;
    if ($y1 == $y2) {
        my $char = $bracket eq "{" ? "⎨" :
                   $bracket eq "}" ? "⎬" :
                   $bracket;
        set_substr_at_yx($char, $y1, $x);
    } elsif ($y1 + 1 == $y2 && $bracket eq "{") {
        set_substr_at_yx("⎰", $y1, $x); # height 2
        set_substr_at_yx("⎱", $y2, $x);
    } elsif ($y1 + 1 == $y2 && $bracket eq "}") {
        set_substr_at_yx("⎱", $y1, $x); # height 2
        set_substr_at_yx("⎰", $y2, $x);
    } elsif ($y2 > $y1) {
        my $middle_y = $y1 + int(($y2 - $y1) / 2);
        my $r = $y1;
        set_substr_at_yx($$gfx[0][$i], $r++, $x);    # $r == $y1       # ⎫
        set_substr_at_yx($$gfx[1][$i], $r++, $x) while $r < $middle_y; # ⎪
        set_substr_at_yx($$gfx[2][$i], $r++, $x) if    $r < $y2;       # ⎬
        set_substr_at_yx($$gfx[3][$i], $r++, $x) while $r < $y2;       # ⎪
        set_substr_at_yx($$gfx[4][$i], $r,   $x);    # $r == $y2       # ⎭
    }
}

sub draw_brackets_at_selection($) {
    my $bracket = shift; # symbolic; one of (, ), {, }, [, ]
    state $bracket_pair = {"(" => "()", ")" => "()",
                           "[" => "[]", "]" => "[]",
                           "{" => "{}", "}" => "{}"};
    if ($x1 == $x2 || !defined $bracket_pair->{$bracket}) {
        draw_bracket_at_yyx($bracket, $y1, $y2, $x1);
    } else {
        # If more than one column is selected, draw a pair "{  }".
        my ($left, $right) = split //, $bracket_pair->{$bracket};
        draw_bracket_at_yyx($left,  $y1, $y2, $x1);
        draw_bracket_at_yyx($right, $y1, $y2, $x2);
    }
}

sub message {
    $message = shift;
}

#                          ▒▒▒▒▒▒▒▒▒▒▒▒▒▒▒▒▒▒▒▒▒▒
#                          ▒░░░░░░░░░░░░░░░░░░░░▒
#                          ▒░░  ██  ██  ██  ██░░▒
#                          ▒░░██  ██  ██  ██  ░░▒
#                          ▒░░  ██  ██  ██  ██░░▒
#                          ▒░░██  ██  ██  ██  ░░▒
#                          ▒░░  ██  ██  ██  ██░░▒
#                          ▒░░██  ██  ██  ██  ░░▒
#                          ▒░░  ██  ██  ██  ██░░▒
#                          ▒░░██  ██  ██  ██  ░░▒
#                          ▒░░░░░░░░░░░░░░░░░░░░▒
#                          ▒▒▒▒▒▒▒▒▒▒▒▒▒▒▒▒▒▒▒▒▒▒

sub handle_stoke_selector_input($) {
    $_ = shift;
    if (/^(?:\t|\r| |\e\e)$/) {
        # Selector off
        $selector = undef;
        print "\e[?25h"; # show the cursor
    } elsif (/^\e\[(1;(\d)|)([A-D])$/) {
        if ($3 eq "C") {
            $selected_stroke++
                if $selected_stroke < @$stroke_buttons - 1;
        } elsif ($3 eq "D") {
            $selected_stroke-- if $selected_stroke > 0;
        }
    }
}

# Returns the selection as an array-ref of strings, padded to equal length
sub copy_selection() {
    return undef if $x1 >= $x2 && $y1 >= $y2; # 1x1 selection or none
    my $floating_lines = [];
    # exclude outward connections
    # ┼─┼───┼      ┌─┬───┐
    # ┼─┼───┼  =>  ├─┼───┤
    # │ │   │      │ │   │
    # ┼─┼───┼      └─┴───┘
    my $last_in_row = $x2 - $x1;
    my $width = $last_in_row + 1;
    for (my $r = $y1; $r <= $y2; $r++) {
        my $row = get_substr_at_yx($r, $x1, $width);
        my @chars = split //, $row;
        for (my $i = 0; $i <= $last_in_row; $i++) {
            if ($r == $y1 || $r == $y2 || $i == 0 || $i == $last_in_row) {
                my $c = $chars[$i];
                my $decomp = $decomposed{$c};
                if (defined $decomp) {
                    substr($decomp, 0, 1, " ") if $r == $y1;          # up
                    substr($decomp, 1, 1, " ") if $r == $y2;          # down
                    substr($decomp, 2, 1, " ") if $i == $last_in_row; # right
                    substr($decomp, 3, 1, " ") if $i == 0;            # left
                    $c = $composed{$decomp};
                    $chars[$i] = $c if defined $c;
                }
            }
        }
        $row = join '', @chars;
        push @$floating_lines, $row;
    }
    return $floating_lines;
}

sub paste_at_selection($) {
    my $strs = shift;
    # TODO: Merge with existing outer box drawing parts
    my $r = $y1;
    for (my $i = 0; $i < @$strs; $i++) {
        my $s = $$strs[$i];
        my $debug = $s;
        $debug =~ s/\e/\\e/g;
        $debug =~ s/([\x00-\x1f])/"(" . ord($1) . ")"/eg;
        debug("Pasting '$debug' at $r:$x1\n");
        set_substr_at_yx($s, $r++, $x1);
    }
}

sub move_selection_left() {
    for (my $r = $y1; $r <= $y2; $r++) {
        my $str = get_substr_at_yx($r, $x1, $x2 - $x1 + 1);
        set_substr_at_yx("$str ", $r, $x1 - 1);
    }
}


# Computes a filler, given four decomposed characters
# (Used for experimental 'move' mode)
sub make_filler($$$$) {
    my ($above, $below, $rightof, $leftof) = (@_);
    my $down  = $below   && $below   =~ /^(\S)...$/ ? $point_down{$1}  : " ";
    my $up    = $above   && $above   =~ /^.(\S)..$/ ? $point_up{$1}    : " ";
    my $left  = $leftof  && $leftof  =~ /^..(\S).$/ ? $point_left{$1}  : " ";
    my $right = $rightof && $rightof =~ /^...(\S)$/ ? $point_right{$1} : " ";
    my $decomp = $up.$down.$right.$left;
    my $c = $composed{$decomp};
    $c = " " unless defined $c;
    debug("Make filler: '$decomp' composed to '$c'\n");
    return $c;
}

# Experimental 'move' mode. Only <move left> works and is incomplete.
sub handle_move_input($) {
    $_ = shift;
    if (/^(?:\t|\r| |m|q|\e\e)$/) {
        # Move mode off
        $mode = 'draw';
        message("Drawing mode");
        print "\e[?25h"; # show the cursor
    } elsif (/^\e\[(?:1;\d)?([A-D])$/) {
        if ($1 eq "A" && $y1 > 1) {
            # $move_y--;
        } elsif ($1 eq "B" && $y2 < $lines - 1) {
            # $move_y++;
        } elsif ($1 eq "C" && $x2 < $cols) {
            # $move_x++;
        } elsif ($1 eq "D" && $x1 > 1) {
            # Move selection left
            # Change the corners
            my $nw = get_decomposed_at_yx($y1, $x2);
            if ($nw && $nw =~ /(\S)(.).(.)/) {
                debug("nw = '$nw';  => '" .
                      " " . $2 . $point_right{$1} . $3 . "'\n");
                $nw = " " . $2 . $point_right{$1} . $3;
            }
            my $sw = get_decomposed_at_yx($y2, $x2);
            if ($sw && $sw =~ /(.)(\S).(.)/) {
                debug("sw = '$sw';  => '" .
                      $1 . " " . $point_right{$2} . $3 . "'\n");
                $sw = $1 . " " . $point_right{$2} . $3;
            }
            # Make fillers
            my @fillers = ();
            for (my $r = $y1; $r <= $y2; $r++) {
                my $above   = $r == $y1
                              ? get_decomposed_at_yx($r - 1, $x2)
                              : " ";
                my $below   = $r == $y2
                              ? get_decomposed_at_yx($r + 1, $x2)
                              : " ";
                my $rightof = get_decomposed_at_yx($r, $x2 + 1);
                my $leftof  = $r == $y1 ? $nw :
                              $r == $y2 ? $sw :
                              get_decomposed_at_yx($r, $x2);
                push @fillers, make_filler($above, $below, $rightof, $leftof);
            }
            for (my $r = $y1; $r <= $y2; $r++) {
                my $str = get_substr_at_yx($r, $x1, $x2 - $x1 + 1);
                my $filler = shift @fillers;
                set_substr_at_yx($str.$filler, $r, $x1 - 1);
            }
            set_substr_at_yx($composed{$nw}, $y1, $x2-1) if $nw && $composed{$nw};
            set_substr_at_yx($composed{$sw}, $y2, $x2-1) if $sw && $composed{$sw};
            $selx--; $x--;
        }
        update_selection;
    }
}

sub handle_text_input($) {
    $_ = shift;
    message("");
    if ($_ eq "\e[2~" || $_ eq "\e\e") {
        # INSERT or ESC-ESC exits text mode
        $mode = 'draw';
    } elsif (/^\e\[([A-D])$/) {
        my $move = $1;
        if ($move eq "A") {
            $y-- if $y > 1;
        } elsif ($move eq "B") {
            $y++ if $y < $lines;
        } elsif ($move eq "C") {
            $x++ if $x < $cols;
        } elsif ($move eq "D") {
            $x-- if $x > 1;
        }
        $selx = $x; $sely = $y;
        update_selection();
    } elsif ($_ eq "\r") {
        # Search backward for normal text to find starting column
        my $line = get_substr_at_yx($y, 1, $x - 1);
        if ($line =~ /[\p{Letter}\p{Number}\p{Punct}]+
                      (?:
                           \s{1,2}
                           [\p{Letter}\p{Number}\p{Punct}]+
                      )*\s{0,2}$/x) {
            $x = length($`) + 1;
        }
        $y++ if $y < $lines;
        $selx = $x; $sely = $y;
        update_selection();
    } elsif ($_ eq "\x7f" || $_ eq "\b") {
        $x-- if $x >= 1; # backspace
        set_char(" ");
    } elsif ($_ eq "\e[3~") {
        set_char(" "); # DELETE = SPACE (in overwrite mode)
    } elsif (ord($_) >= 32) {
        # printable character (with unicode exceptions)
        set_char($_);
        $x++ if $x < $cols;
    }
}

# sends an array ref to xclip, if it's installed
sub copy_to_xclip($) {
    open my $XCLIP, '|-', 'xclip', '-selection', 'clipboard' or do {
        debug("Couldn't open pipe to xclip: $!");
        message("Install xclip to use the X clipboard");
        return;
    };
    my $lines_ref = shift;
    print $XCLIP join("\n", @$lines_ref);
    close $XCLIP;
    message("Copied to X clipboard");
}

# returns an array ref of lines from the clipboard
sub paste_from_xclip() {
    open my $XCLIP, '-|', 'xclip', '-o', '-selection', 'clipboard' or do {
        debug("Couldn't open pipe to xclip: $!");
        message("Install xclip to use the X clipboard");
        return;
    };
    my $lines = [];
    while (<$XCLIP>) {
        chomp;
        push @$lines, $_;
    }
    close $XCLIP;
    message("Pasted from X clipboard");
    return $lines;
}

sub select_stroke($) {
    my $name = shift;
    for (my $i = 0; $i < @$stroke_buttons; $i++) {
        if ($$stroke_buttons[$i][2] eq $name) {
            $selected_stroke = $i;
            return;
        }
    }
    die "Stroke $name doesn't exist"; # shouldn't happen
}

# Read input until the end-of-paste escape sequence and return the lines
sub get_bracketed_paste() {
    my $lines = [];
    my $line = "";
    while (1) {
        $_ = get_input_sequence();
        if (/^[\e]/) {
            last if $_ eq "\e[201~";
            s/\e/\\e/g;
            debug("Unexpected escape sequence in bracketed paste mode: '$_'\n");
        } elsif (/[\r\n\v\f]/) {
            push @$lines, $line;
            $line = "";
        } elsif ($_ eq "\t") { # add spaces up to a column divisible by 8
            $line .= " " x (8 - (length($line) % 8));
        } elsif (/^[\x00-\x1f]$/) {
            my $ord = ord($_);
            debug(sprintf("Unexpected control character " .
                          "in bracketed paste mode: ^%s (0x%x)\n",
                          chr($ord + 64), $ord));
        } else {
            $line .= $_;
        }
    }
    push @$lines, $line if $line ne "";
    return $lines if @$lines;
}

# input in drawing mode when no stroke selector is open
sub handle_input($) {
    $_ = shift;
    my $sel_on = 0;
    my $pen = 0;
    my $old_x = $x;
    my $old_y = $y;
    my $move = undef; # ABCD directions (up, down, right, left)
    message("");
    if (/^\e\[(1;(\d)|)([A-D])$/) {
        # Arrow key
        if (!$1) {
            # No modifier, just move cursor
        } elsif ($2 == 2) {
            # Shift: Start selection unless already started
            if (!defined $selx) {
                $sely = $y; $selx = $x;
            }
            $sel_on = 1;
        } elsif ($2 == 5) {
            # Ctrl: Pen using selected stroke
            $pen = 1;
        } else {
            debug("Arrow key with modifier $2\n")
        }
        $move = $3;
    } elsif ($_ eq "\e[3~" || $_ eq "\x7f" || $_ eq "\b") {
        # delete or backspace
        fill_selection(" ");
    } elsif ($_ eq "\e\e") {
        deselect();
    } elsif ($_ eq "\x1a") {
        message("Undo is not implemented");
    } elsif ($_ eq "\x18") {
        my $copied_lines = copy_selection();
        if ($copied_lines) {
            copy_to_xclip($copied_lines);
            # TODO: Keep outer box drawing parts
            fill_selection(" ");
        }
    } elsif ($_ eq "\x03") {
        my $copied_lines = copy_selection();
        if ($copied_lines) {
            copy_to_xclip($copied_lines);
        } else {
            message("Press q to quit");
        }
    } elsif ($_ eq "\x16") {
        my $paste_lines = paste_from_xclip();
        paste_at_selection($paste_lines) if $paste_lines;
    } elsif ($_ eq "x") {
        draw_diagonal("X");
    } elsif ($_ eq "/") {
        draw_diagonal("/");
    } elsif ($_ eq "\\") {
        draw_diagonal("\\");
    } elsif (/^[{}()\[\]S]$/) {
        # {}()[]S (where S is a large integral sign)
        draw_brackets_at_selection($_);
    } elsif ($_ eq "-") {
        select_stroke('single');
        draw_borders_at_selection("single");
    } elsif ($_ eq "o") {
        select_stroke('single-rounded');
        draw_borders_at_selection("single-rounded");
    } elsif ($_ eq "b") {
        select_stroke('bold');
        draw_borders_at_selection("bold");
    } elsif ($_ eq "=") {
        select_stroke('double');
        draw_borders_at_selection("double");
    } elsif ($_ eq "1") {
        toggle_subblock("▖");
    } elsif ($_ eq "2") {
        toggle_subblock("▄", "▁");
    } elsif ($_ eq "3") {
        toggle_subblock("▗");
    } elsif ($_ eq "4") {
        toggle_subblock("▌", "▏");
    } elsif ($_ eq "5") {
        rotate_fill_selection("█", "▓", "▒", "░", " ");
    } elsif ($_ eq "6") {
        toggle_subblock("▐", "▕");
    } elsif ($_ eq "7") {
        toggle_subblock("▘");
    } elsif ($_ eq "8") {
        toggle_subblock("▀", "▔");
    } elsif ($_ eq "9") {
        toggle_subblock("▝");
    } elsif ($_ eq "u") {
        # Upward diagram
        rotate_fill_selection("▁", "▂", "▃", "▄", "▅", "▆", "▇", "█", " ");
    } elsif ($_ eq "U") {
        # Upward diagram, backwards
        rotate_fill_selection("█", "▇", "▆", "▅", "▄", "▃", "▂", "▁", " ");
    } elsif ($_ eq "r") {
        # Left-to-right diagram
        rotate_fill_selection("▏", "▎", "▍", "▌", "▋", "▊", "▉", "█", " ");
    } elsif ($_ eq "R") {
        # Left-to-right diagram, backwards
        rotate_fill_selection("█", "▉", "▊", "▋", "▌", "▍", "▎", "▏", " ");
    } elsif ($_ eq "<") {
        rotate_fill_selection("<", "◀", "◁", "≪"); # "◄"
    } elsif ($_ eq ">") {
        rotate_fill_selection(">", "▶", "▷", "≫"); # "►"
    } elsif ($_ eq "v") {
        rotate_fill_selection("∨", "▼", "▽", "V"); # "⌄"
    } elsif ($_ eq "^") {
        rotate_fill_selection("∧", "▲", "△", "Λ"); # "⌃"
    } elsif ($_ eq ".") {
        rotate_fill_selection("●", "•", "·", "○", "◉", "◎", "◍");
    } elsif ($_ eq ",") {
        rotate_fill_selection(qw(◆ ◇ ◈ ■ □ ▢ ▣ ⊞ ⊟ ⊠ ⊡));
    } elsif ($_ eq "*") {
        rotate_fill_selection(qw(⋆ ∗ × ⨂ ⨁ ⨀ ⍟ ⊕ ⊖ ⊗ ⊘ ⊙ ⊚ ⊛ ⊜ ⊝));
    } elsif ($_ eq "g") {
        rotate_fill_selection("⏚", "⏛", "⏀", "⏁", "⏂", "⏃", "⏄", "⏅",
                              "⏆", "⏇", "⏈", "⏉", "⏊");
    } elsif ($_ eq "e") {
        # Emoji test. These should be full-width, but some aren't.
        # set_char("\x{1F600}"); # :D full-width
        set_char("\x{263a}"); # :-) half-width
    } elsif ($_ eq "\0") {
        # Ctrl+Space = start sticky selection
        $sticky_selection = 1;
        $selx = $x;
        $sely = $y;
        update_selection;
    } elsif ($_ eq " ") {
        # Space = select/deselect
        if ($y2 > $y1 || $x2 > $x1 || $sticky_selection) {
            deselect();
        } else {
            # Start sticky select
            $sticky_selection = 1;
            $selx = $x;
            $sely = $y;
            update_selection;
        }
    } elsif ($_ eq "\r") {
        if ($y2 > $y1 || $x2 > $x1) {
            my $stroke = $$stroke_buttons[$selected_stroke][2];
            draw_borders_at_selection($stroke);
        }
    } elsif ($_ eq "\t") {
        # toggle stroke selector
        # (private use CSI)
        # CSI ? 25 h    DECTCEM Shows the cursor, from the VT320.
        # CSI ? 25 l    DECTCEM Hides the cursor.
        $selector = 'stroke';
        print "\e[?25l"; # hide cursor
    } elsif ($_ eq "m") {
        # Switch to 'move' mode # if there is a selection larger than 1x1
        message("Move selected area using the arrow keys, then RETURN");
        $mode = 'move'; # if ($x2 > $x1 || $y2 > $y1);
    } elsif ($_ eq "t" || $_ eq "\e[2~") {
        $mode = 'text'; # t or INSERT
    } elsif ($_ eq "q") {
        #debug("q=quit\n");
    } elsif ($_ eq "h") {
        message("Press h again to close the help box");
        print "\e[?25l"; # hide cursor
        $mode = "help";
    } elsif ($_ eq "\e[200~") {
        my $strs = get_bracketed_paste();
        if ($strs) {
            paste_at_selection($strs);
            message("Pasted using terminal emulator");
        }
    } elsif (/^\e/) {
        s/\e/\\e/g;
        debug("Unhandled escape sequence: $_\n");
    } elsif (/^[\x00-\x1f]$/) {
        my $ord = ord($_);
        debug(sprintf("Unhandled control character: ^%s (0x%x)\n",
              chr($ord + 64), $ord));
    } else {
        debug(sprintf("Unhandled character: %s (0x%x)\n", $_, ord($_)));
    }

    # Move cursor
    if (defined $move) {
        if ($move eq "A") {
            $y-- if $y > 1;
        } elsif ($move eq "B") {
            $y++ if $y < $lines - 1;
        } elsif ($move eq "C") {
            $x++ if $x < $cols;
        } elsif ($move eq "D") {
            $x-- if $x > 1;
        }
        unless ($sel_on || $sticky_selection) {
            #$selx = $sely = undef; # no selection
            $selx = $x; $sely = $y; # 1x1 selection
        }
        if ($pen) {
            # draw from $old_y, $old_x to current
            draw_selected_stroke_at($old_y, $old_x, $y, $x);
        }
        update_selection();
    }
}

# Fetch the rest of an escape sequence starting with the Control Sequence
# Introducer. "\e[" has already been received.
sub handle_csi {
    my $param = "";
    my $intermediate = "";
    my $c;
    $c = getc();
    while ($c ge "0" && $c le "?") {
        # "parameter bytes" in the range 0x30–0x3F (ASCII 0–9:;<=>?)
        $param .= $c;
        $c = getc();
    }
    while ($c ge " " && $c le "/") {
        # "intermediate bytes" in the range 0x20–0x2F (ASCII space and !"#$%&'()*+,-./)
        $intermediate .= $c;
        $c = getc();
    }
    # a single "final byte" in the range 0x40–0x7E (ASCII @A–Z[\]^_`a–z{|}~)
    $c ge "@" && $c le "~" or die "Invalid escape sequence";
    return "\e[$param$intermediate$c";
}

# Handle escape sequences, i.e. sequences starting with \e
sub handle_esc {
    my $c = getc();
    if ($c eq "[") {
        return handle_csi();
    } elsif ($c eq "O") {
        # \eOP \eOQ \eOR \eOS = F1 F2 F3 F4
        my $F = getc();
        return $F unless $F =~ /^[PQRS]$/;
        return "\e$c$F";
    } else {
        return "\e$c";
    }
}

# A wrapper for getc(), fetching a character or a complete escape sequence
sub get_input_sequence() {
    my $c = getc();
    die "getc(): $!" unless defined $c;
    if ($c eq "\e") {
        return handle_esc();
    } else {
        return $c;
    }
}

system qw(stty raw -echo);
print "\e[?2004h\n"; # bracketed paste mode on
update_window_size();
# center cursor
$x1 = $x2 = $x = int($cols / 2);
$y1 = $y2 = $y = int($lines / 2);
if ($show_splash) {
    render();
    render_splash();
    get_input_sequence();
}
while(1) {
    render();
    my $c = get_input_sequence();
    if ($mode eq "help") {
        # handle input when help box is open
        if ($c =~ /^(?:h|q|\e\e)$/) {
            $mode = 'draw';
            print "\e[?25h"; # show cursor
            message("");
        } elsif ($c =~ /^(?:n| |\e\[B|\e\[C|\e\[6~)$/) {
            # n, SPACE, DOWN, RIGHT, PAGE DOWN
            $help_page_no++;
        } elsif ($c =~ /^(?:p|\e\[A|\e\[D|\e\[5~)$/) {
            # p, UP, LEFT, PAGE UP
            $help_page_no--;
        }
    } elsif ($mode eq 'move') {
        handle_move_input($c);
    } elsif ($mode eq 'text') {
        handle_text_input($c);
    } elsif (!defined($selector)) {
        handle_input($c);
        last if $c eq "q";
    } elsif ($selector eq 'stroke') {
        handle_stoke_selector_input($c);
    }
}
